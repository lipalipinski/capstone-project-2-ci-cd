def image_tag
pipeline {
    agent {label 'ec2'}

    environment {
        // CI repo dir name
        CI_REPO="CI-REPOSITORY"
        TF_CLI_ARGS="-no-color"
    }

    tools {
        // Install Maven
        maven 'maven-3.9.2'
    }

    options { 
        skipDefaultCheckout() 
    }

    stages {
        stage('checkout SCM') {
            steps {
                // checkout app repo
                checkout scm
                // checkout CI repo to <workdir>/"${env.CI_REPO_DIR}"
                checkout scmGit(
                    branches: [[name: 'main']], 
                    extensions: [[$class: 'RelativeTargetDirectory', relativeTargetDir: "${env.CI_REPO}"]], 
                    // CI repo URL from var set by emote Jenkinsfile Provider plugin
                    userRemoteConfigs: [[url: "${env.RJPP_SCM_URL}"]]
                )

                script {
                    // determine docker image tag
                    if (env.BRANCH_NAME == 'main') {
                        def pom = readMavenPom()
                        image_tag = pom.getVersion()
                        env.snapshot = sh(
                              returnStdout: true, 
                              script: "if [[ ${image_tag } =~ '-SNAPSHOT' ]]; then echo 'true'; else echo 'false'; fi"
                          ).trim()
                    } else {
                        image_tag = sh(
                              returnStdout: true, 
                              script: 'git rev-parse --short HEAD'
                          ).trim()
                    }

                    // set custom build name
                    currentBuild.displayName = "#" + currentBuild.number + "-${image_tag}"
                }
            }
        }
        stage('test app') {
            steps {
                echo "Building..."
                sh "mvn --batch-mode clean package"
            }
        }
        stage('build container') {
            steps {
              echo "Building docker container..."
              sh "docker build -q -t jlipinski-petclinic:${image_tag} ."
            }
        }
        stage('push container') {
            steps {
              withAWSParameterStore(credentialsId: '', naming: 'basename', path: '/jlipinski', recursive: true, regionName: 'eu-central-1') {
                echo "Docker login to ECR..."
                sh "aws ecr get-login-password --region eu-central-1 | docker login --username AWS --password-stdin $ECR_REGISTRY_URL"
                echo "Tagging the image..."
                sh "docker tag jlipinski-petclinic:${image_tag} $ECR_REGISTRY_URL:${image_tag}"
                echo "Pushing container to ECR..."
                sh "docker push -q $ECR_REGISTRY_URL:${image_tag}"
              }
            }
        }
        stage('tag commit') {
          when {
            allOf {
              branch 'main'
              environment name: 'snapshot', value: 'false'
            }
          }
          steps {
            withCredentials([gitUsernamePassword(credentialsId: 'jenkins-petclinic-token-user', gitToolName: 'Default')]) {
              sh "git tag ${image_tag}"
              sh "git push origin ${image_tag}"
            }
          }
        }
    }
}